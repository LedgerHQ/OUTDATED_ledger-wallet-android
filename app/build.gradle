apply plugin: "com.android.application"
apply plugin: "jp.leafytree.android-scala"

android {
    compileSdkVersion 23

    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "co.ledger.wallet"
        targetSdkVersion 23
        versionCode 7
        versionName "1.0.4"
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "2g"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }


    productFlavors {
        dev {
            applicationId "co.ledger.wallet.dev"
            resValue "string", "app_name", "Ledger Wallet (Dev)"
            minSdkVersion 22
        }

        beta {
            applicationId "co.ledger.wallet.beta"
            resValue "string", "app_name", "Ledger Wallet (Beta)"
            minSdkVersion 14
        }

        production {
            minSdkVersion 14
        }
    }

    compileOptions {
    }
}

repositories {
    maven {
        url 'http://releases.marmeladburk.fidesmo.com'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:preference-v14:23.1.0'
    compile 'org.scala-lang:scala-library:2.11.7'
    compile 'me.dm7.barcodescanner:zbar:1.6'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:cardview-v7:23.0.1'
    compile 'com.koushikdutta.async:androidasync:2.1.6'
    compile 'com.android.support:multidex:1.0.0'
    compile 'com.android.support:design:23.0.1'
    compile 'com.madgag.spongycastle:core:1.51.0.0'
    compile 'com.madgag.spongycastle:prov:1.51.0.0'
    compile 'com.madgag.spongycastle:pkix:1.51.0.0'
    compile 'com.madgag.spongycastle:pg:1.51.0.0'
    compile 'com.google.android.gms:play-services:3.1.+'
    compile 'com.netaporter:scala-uri_2.11:0.4.7'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.daimajia.easing:library:1.0.1@aar'
    compile 'com.daimajia.androidanimations:library:1.1.3@aar'
    compile 'org.bitcoinj:bitcoinj-core:0.13.4'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.google.zxing:core:3.2.0'
    compile 'com.android.support:preference-v7:23.0.1'
    compile group: 'com.fidesmo', name: 'nordpol-android', version: '0.1.4', ext: 'aar', transitive: true
    compile 'com.google.guava:guava:19.0-rc1'
    androidTestCompile 'com.android.support:multidex-instrumentation:1.0.1'
    testCompile 'junit:junit:4.12'
    debugCompile 'com.facebook.stetho:stetho:1.2.0'
    //compile 'org.slf4j:slf4j-android:1.6.1-RC1'
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.deprecation = false
    scalaCompileOptions.useCompileDaemon = true
}

tasks.withType(ScalaCompile) {
    configure(scalaCompileOptions.forkOptions) {
        memoryMaximumSize = '1g'
        jvmArgs = ['-XX:MaxPermSize=512m']
    }
}
tasks.withType(ScalaCompile) {
    scalaCompileOptions.useAnt = false
}


afterEvaluate {
    tasks.matching {
        it.name.startsWith("dex")
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += "--multi-dex"
        dx.additionalParameters += "--main-dex-list=$rootDir/main-dex-list.txt".toString()
    }
}
